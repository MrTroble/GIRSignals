buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
		maven {
      		url = "https://plugins.gradle.org/m2/"
    	}
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.1+'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

apply plugin: "com.matthewprenger.cursegradle"

sourceSets{
    main {
        java {
            srcDirs("guilib/src/", "linkableapi/src/", "contentPackLib/src")
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }



def ver = System.getenv("APPVEYOR_BUILD_VERSION")
if(ver == null)
	ver = 'dev'

def key = System.getenv("CURSETOKEN")
if(key == null)
	key = 'dev'
curseforge {
  apiKey = key
  project {
    id = '440990'
    changelog = file('changelog.md')
    releaseType = 'beta'
    changelogType = 'markdown'
    addGameVersion '1.14.4'
  }
  options {
    detectNewerJava = true
  }
}

version = ver
group = 'com.troblecodings'
archivesBaseName = 'OpenSignals-1.14.4'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
	mappings channel: 'official', version: '1.14.4'
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {   
    minecraft 'net.minecraftforge:forge:1.14.4-28.2.26'
    testImplementation("org.junit.jupiter:junit-jupiter:5.9.1")
}

test {
    useJUnitPlatform {
    	includeEngines("junit-jupiter")
    }
}

processResources {
	inputs.property "version", project.version
	
	filesMatching("mcmod.info") {
		expand "version": project.version, "mcversion": "1.14.4"
	}
}

jar {
    manifest {
        attributes([
            "Specification-Title": archivesBaseName,
            "Specification-Vendor": "troblecodings",
            "Specification-Version": ver,
            "Implementation-Title": project.name,
            "Implementation-Version": ver,
            "Implementation-Vendor" :"troblecodings",
            "Implementation-Timestamp": new Date().format("dd-MM-yyyy'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')
