buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
		maven {
      		url = "https://plugins.gradle.org/m2/"
    	}
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.1+'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

apply plugin: "com.matthewprenger.cursegradle"

sourceSets{
    main {
        java {
            srcDirs("guilib/src/", "linkableapi/src/")
        }
    }
}


def ver = System.getenv("APPVEYOR_BUILD_VERSION")
if(ver == null)
	ver = 'dev'

def key = System.getenv("CURSETOKEN")
if(key == null)
	key = 'dev'
curseforge {
  apiKey = key
  project {
    id = '440990'
    changelog = file('changelog.md')
    releaseType = 'release'
    changelogType = 'markdown'
    mainArtifact(jar) {
      relations {
        optionalDependency 'opencomputers'
      }
    }
  }
}

version = ver
group = 'net.gir.girsignals'
archivesBaseName = 'GIRSignals'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    maven{ url="https://maven.cil.li/" }
}

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

def ocVersion = "MC1.12.2-1.7.5.218"
def copyName = ""

dependencies {   
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
    compileOnly "li.cil.oc:OpenComputers:$ocVersion:api"
    runtimeOnly "li.cil.oc:OpenComputers:$ocVersion"
    testImplementation("org.junit.jupiter:junit-jupiter:5.9.1")
}

test {
    useJUnitPlatform {
    	includeEngines("junit-jupiter")
    }
}

processResources {
	inputs.property "version", project.version
	
	filesMatching("mcmod.info") {
		expand "version": project.version, "mcversion": "1.12.2"
	}
}

jar {
    manifest {
        attributes([
            "Specification-Title": "GIRSignals",
            "Specification-Vendor": "German Immersive Railroading Community",
            "Specification-Version": ver,
            "Implementation-Title": project.name,
            "Implementation-Version": ver,
            "Implementation-Vendor" :"German Immersive Railroading Community",
            "Implementation-Timestamp": new Date().format("dd-MM-yyyy'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.getByName("eclipse").finalizedBy('update')
task update(type: Copy) {
	dependsOn "eclipse"
	def username = System.getProperty("user.home")
    println("$username/.gradle/caches/modules-2/files-2.1/li.cil.oc/OpenComputers/$ocVersion/")
    def fl = new File("$username/.gradle/caches/modules-2/files-2.1/li.cil.oc/OpenComputers/$ocVersion/")
    if(fl.exists()) {
    	fl.eachDir() {
        	dire -> dire.eachFileMatch("OpenComputers-$ocVersion" + ".jar") {
         	   file2 -> copyName = file2.getAbsolutePath() 
        	}
    	}
        def filef = new File('run/mods')
        if(filef.exists()) {
		    filef.eachFile {
		    	file -> file.delete()
			}	
			if(copyName != "") {
    	    	from copyName
    	    	into 'run/mods'
    		}
        }
    }
}
